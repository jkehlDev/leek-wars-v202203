global _IA_dd_magic_init = false;
global _IA_dd_magic_cards = [];

function _IA_DD_magic() {
	out.info('ROLE_DD_MAGIC');
	displayContext();

	// - --- ---------------------------

	// - INIT HEALER CARDS
	if (!_IA_dd_magic_init) {
		var _tpMax = myLeek().leek_tpMax + 6;
		_IA_dd_magic_init = true;
		_IA_dd_magic_cards = [
			'BUFF': new ToolCard([], [CHIP_COVETOUSNESS, CHIP_WIZARDRY, CHIP_ADRENALINE, CHIP_MOTIVATION], _tpMax),
		];
		myLeek().setWeaponById(myLeek().leek_weapons[0]);
	}

	// - --- ---------------------------
	var tacticalMove = DD_MAGIC_Resolve_TACTICAL_MOVE();
	var _isJump = GBL_Resolve_JUMP(tacticalMove.isPriority, tacticalMove.targetCell);
	if (_isJump) {
		tacticalMove = DD_MAGIC_Resolve_TACTICAL_MOVE();
	}
	// - --- ---------------------------
	GBL_Resolve_KILL_SHOOT(0);
	GBL_Resolve_HEAL_EMERGENCY();
	GBL_Resolve_ABSOLUTE_SHIELD(tacticalMove.cell_id);
	DD_MAGIC_Resolve_BUFF(0);
	GBL_Resolve_POISON();
	GBL_Resolve_DEBUFF(4);
	GBL_Resolve_WEAPONS();
	GBL_Resolve_DEBUFF(0);
		
	// - --- ---------------------------
	GBL_Resolve_JUMP_TO(tacticalMove.isPriority, tacticalMove.targetCell);
	tacticalMove = DD_MAGIC_Resolve_TACTICAL_MOVE();
	tacticalMove.moveTo();
	// - --- ---------------------------
}
// --- ---------------------------------------------------------------------------------------------
function DD_MAGIC_Resolve_TACTICAL_MOVE() {
	var myLeekState = getLeekStateById(GBL_myLeek_id);
	var _tacticalMove;
	_tacticalMove = resolveTacticalPositionByTargetsLeeksNdTacticalArea(
		getLeeksByTypeNdRole(LEEK_TYPE_ALLY, LEEK_ROLE_TANK),
		_sortLeekByLevelDSC,
		GBL_effect_area_template_by_area_type_id[AREA_CIRCLE_2_4],
		_sortPositionsNearestTarget
	)

	if (_tacticalMove.cell_id === myLeek().leek_cell) {
		_tacticalMove = resolveTacticalPositionByTargetsLeeksNdTacticalArea(
			getAllEnemiesAliveLeeks(),
			_sortLeekByLifeRateASC,
			GBL_effect_area_template_by_area_type_id[AREA_CIRCLE_6_15],
			_sortPositionsSafest
		);
	}
	return _tacticalMove;
}
// --- ---------------------------------------------------------------------------------------------
function DD_MAGIC_Resolve_BUFF(_tpMalus) {
	var _selectedOptions = _IA_dd_magic_cards['BUFF'].getOptionsBy(
		[GBL_filter_toolCardOption_by_cost(myLeek().leek_tp - _tpMalus)],
		GBL_sort_toolCardOption_by_cost_dsc, []);
	var _isExecuted = false;
	var _excludeTools = [];
	var _currentOption, _tool_id, _contract, _launchs, _selected_launch;
	while (count(_selectedOptions) > 0 && !_isExecuted) {
		_currentOption = shift(_selectedOptions);
		for (_tool_id in _currentOption._cast_array) {
			if (_tool_id === CHIP_COVETOUSNESS) {
				_contract = new LaunchContract(
					launch_resolvers.CAST_ON_TARGET,
					[
						GBL_launch_filters_before_linearize.TARGET_AT_LEAST_X_TYPE_OF_LEEKS([{
							typesOf: LEEK_TYPE_ENEMY | LEEK_TYPE_ENEMY_SUMMON,
							count: 2
						}]),
					],
					[],
					[launch_sorters.SORT_MAX_EFFECT_VALUE, launch_sorters.SORT_MIN_CAST_DISTANCE],
					_tool_id);
			} else {
				_contract = new LaunchContract(
					launch_resolvers.CAST_AROUND,
					[
						GBL_launch_filters_before_linearize.TARGET_ONLY_CASTER,
						GBL_launch_filters_before_linearize.TARGET_NOT_EMPTY
					],
					[],
					[],
					_tool_id);
			}
			_launchs = new Launchs(_contract);
			if (count(_launchs.selected_launchs) > 0) {
				_isExecuted = true;
				_launchs.selected_launchs[0].launch();
			} else {
				_isExecuted = false;
				push(_excludeTools, _tool_id);
				_selectedOptions = _IA_dd_magic_cards['BUFF'].getOptionsBy(
					[GBL_filter_toolCardOption_by_cost(myLeek().leek_tp - _tpMalus)],
					GBL_sort_toolCardOption_by_cost_dsc, _excludeTools);
				break;
			}
		}
	}
}