/*
 * constructor(touched_leeks, countLeeks, caster_leek_id, cast_cell_id, cast_cell_data, cast_cell_distance, target_cell_id, target_cell_data, tool_id, effects_value)
 * */
class LaunchItem {
	touched_leeks;
	countLeeks;
	caster_leek_id;
	cast_cell_id;
	cast_cell_data;
	cast_cell_distance;
	target_cell_id;
	target_cell_data;
	tool_id;
	effects_value;

	constructor(touched_leeks, countLeeks, caster_leek_id, cast_cell_id, cast_cell_data, cast_cell_distance, target_cell_id, target_cell_data, tool_id, effects_value) {
		this.touched_leeks = touched_leeks;
		this.countLeeks = countLeeks;
		this.caster_leek_id = caster_leek_id;
		this.cast_cell_id = cast_cell_id;
		this.cast_cell_data = cast_cell_data;
		this.cast_cell_distance = cast_cell_distance;
		this.target_cell_id = target_cell_id;
		this.target_cell_data = target_cell_data;
		this.tool_id = tool_id;
		this.effects_value = effects_value;
	}

	launch() {
		var state = 0;
		var tool = getToolById(this.tool_id)
		var caster = getLeekById(this.caster_leek_id);
		if (caster.leek_cell !== this.cast_cell_id) {
			moveTowardCell(this.cast_cell_id);
			caster.update();
		}
		if (caster.leek_cell === this.cast_cell_id) {
			if (tool.tool_isWeapon) {
				if (caster.setWeaponById(this.tool_id)) {
					state = useWeaponOnCell(this.target_cell_id);
				}
			} else if (tool.tool_isSummon) {
				state = summon(this.tool_id, this.target_cell_id, _main_run);

			} else {
				state = useChipOnCell(this.tool_id, this.target_cell_id);
			}
			var _leek_id;
			for (_leek_id in this.touched_leeks) {
				getLeekById(_leek_id).update();
			}
			getLeekById(caster.leek_id).update();
		}
		if (state > 0) {
			if (arrayInclude([CHIP_INVERSION, CHIP_JUMP, CHIP_GRAPPLE], function(_tool_id) {
					return _tool_id === this.tool_id
				})) {
				_refreshRadarZones();
				GBL_myLeek_current_path = resolvePathMap(myLeek().leek_mp, myLeek().leek_mp, myLeek().leek_cell, GBL_obstacles_map_binary_with_leek_area.map, false);
			}
			if (tool.tool_isSummon) {
				getLeekById(getEntityOnCell(this.target_cell_id));
			}
			out.info('SUCCESS LAUNCH - ' + getToolById(this.tool_id).tool_name +
				', targets :' + this.touched_leeks +
				', target cell [ ' + this.target_cell_id +
				' ] from cell [ ' + this.cast_cell_id + ' ]');
			out.info('Calculated effects : ' + this.effects_value);
		} else {
			out.error('FAIL LAUNCH - [ ' + state + ' ] - targets :' + this.touched_leeks +
				', ' + getToolById(this.tool_id).tool_name +
				', target cell [ ' + this.target_cell_id +
				' ] from cell [ ' + this.cast_cell_id + ' ]');
		}
	}
}